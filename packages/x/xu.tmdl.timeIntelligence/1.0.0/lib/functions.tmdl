/// Returns a formatted informational string about the XU.TMDL.TimeIntelligence DAX UDF library.
function 'XU.TMDL.TimeIntelligence.About' =
		() =>
			VAR LB = UNICHAR(10)
			RETURN
				"ðŸ’¡ Library Name: XU.TMDL.TimeIntelligence" & LB &
				"ðŸ§‘ Author: HD XU" & LB &
				"ðŸ“¦ Version: 1.0.0" & LB &
				"âš–ï¸ License: MIT â€“ Free to use and modify" & LB &
				LB &
				"ðŸ’Ž Functions included:" & LB &
				"â€¢ XU.TMDL.TimeIntelligence.MeasureCreate(MeasureList, DateColumn, MeasureTable) â†’ Generates TMDL script for time intelligence measures" & LB &
				"â€¢ XU.TMDL.TimeIntelligence.Config.NumberFormat() â†’ Default format string for numeric measures" & LB &
				"â€¢ XU.TMDL.TimeIntelligence.Config.PercentFormat() â†’ Default format string for percentage measures" & LB &
				LB &
				"âš ï¸ Important usage instructions:" & LB &
				"1. Create a measure that calls XU.TMDL.TimeIntelligence.MeasureCreate() with your parameters" & LB &
				"2. Place this measure in a table visual to generate and view the TMDL script" & LB &
				"3. Copy the entire output text from the visual" & LB &
				"4. Execute the script using the Power BI TMDL executor to create all time intelligence measures" & LB &
				LB &
				"ðŸ“š Parameter descriptions:" & LB &
				"â€¢ MeasureList: List of base measure names to generate time intelligence calculations for" & LB &
				"â€¢ DateColumn: Reference to the date column (e.g., ""'Calendar'[Date]"")" & LB &
				"â€¢ MeasureTable: Name of table where generated measures will be placed"

	annotation DAXLIB_PackageId = XU.TMDL.TimeIntelligence

	annotation DAXLIB_PackageVersion = 1.0.0

/// Default number format string for all time intelligence measures
function 'XU.TMDL.TimeIntelligence.Config.NumberFormat' = 
		() => "#,0"

	annotation DAXLIB_PackageId = XU.TMDL.TimeIntelligence

	annotation DAXLIB_PackageVersion = 1.0.0

/// Default percentage format string for all time intelligence measures
function 'XU.TMDL.TimeIntelligence.Config.PercentFormat' = 
		() => "#,0.00%;-#,0.00%;#,0.00%"

	annotation DAXLIB_PackageId = XU.TMDL.TimeIntelligence

	annotation DAXLIB_PackageVersion = 1.0.0

/// ðŸ’Ž Description: Generates TMDL script to create time intelligence measures for a list of base measures
/// 1ï¸âƒ£ Parameter - MeasureList: List of base measure names (e.g., {"Sales Amount", "Profit"})
/// 2ï¸âƒ£ Parameter - DateColumn: Date column name (e.g., "'Calendar'[Date]")
/// 3ï¸âƒ£ Parameter - MeasureTable: Name of table where the generated measures will be created (e.g., "Financial Metrics")
/// ðŸ’¡ Note: Formats are controlled by XU.TMDL.TimeIntelligence.Config.NumberFormat() and XU.TMDL.TimeIntelligence.Config.PercentFormat()
/// ðŸ“Œ Example: XU.TMDL.TimeIntelligence.MeasureCreate({"Sales Amount", "Profit"}, "'Calendar'[Date]", "'Financial Metrics'")
function 'XU.TMDL.TimeIntelligence.MeasureCreate' =
		(
			MeasureList : TABLE VAL,
			DateColumn : STRING VAL,
			MeasureTable : STRING VAL
		) =>
			VAR LB = UNICHAR(10)

			// === Get configurable formats ===
			VAR NumberFormat = LB & "            formatString: " & XU.TMDL.TimeIntelligence.Config.NumberFormat()
			VAR PercentFormat = LB & "            formatString: " & XU.TMDL.TimeIntelligence.Config.PercentFormat()
			
			// === Generate TMDL script ===
			VAR ScriptStart =
			"createOrReplace" & LB &
			LB &
			"    ref table " & MeasureTable & LB &
			LB
			
			VAR ScriptMain = 
				CONCATENATEX(
					MeasureList,
					// Year-To-Date | Quarter-To-Date | Month-To-Date
					"       measure '" & [Value] & " YTD' = CALCULATE([" & [Value] & "], DATESYTD(" & DateColumn & "))" & NumberFormat & LB &
					"       measure '" & [Value] & " QTD' = CALCULATE([" & [Value] & "], DATESQTD(" & DateColumn & "))" & NumberFormat & LB &
					"       measure '" & [Value] & " MTD' = CALCULATE([" & [Value] & "], DATESMTD(" & DateColumn & "))" & NumberFormat & LB &
					
					// Previous Year | Previous Quarter | Previous Month
					"       measure '" & [Value] & " PY' = CALCULATE([" & [Value] & "], DATEADD(" & DateColumn & ", -1, YEAR))" & NumberFormat & LB &
					"       measure '" & [Value] & " PQ' = CALCULATE([" & [Value] & "], DATEADD(" & DateColumn & ", -1, QUARTER))" & NumberFormat & LB &
					"       measure '" & [Value] & " PM' = CALCULATE([" & [Value] & "], DATEADD(" & DateColumn & ", -1, MONTH))" & NumberFormat & LB &
					
					// Previous Year Complete | Previous Quarter Complete | Previous Month Complete
					"       measure '" & [Value] & " PYC' = CALCULATE([" & [Value] & "], PARALLELPERIOD(" & DateColumn & ", -1, YEAR))" & NumberFormat & LB &
					"       measure '" & [Value] & " PQC' = CALCULATE([" & [Value] & "], PARALLELPERIOD(" & DateColumn & ", -1, QUARTER))" & NumberFormat & LB &
					"       measure '" & [Value] & " PMC' = CALCULATE([" & [Value] & "], PARALLELPERIOD(" & DateColumn & ", -1, MONTH))" & NumberFormat & LB &
					
					// Year-Over-Year | Quarter-Over-Quarter | Month-Over-Month absolute number
					"       measure '" & [Value] & " YOY Î”' = [" & [Value] & "] - [" & [Value] & " PY]" & NumberFormat & LB &
					"       measure '" & [Value] & " QOQ Î”' = [" & [Value] & "] - [" & [Value] & " PQ]" & NumberFormat & LB &
					"       measure '" & [Value] & " MOM Î”' = [" & [Value] & "] - [" & [Value] & " PM]" & NumberFormat & LB &
					
					// Year-Over-Year % | Quarter-Over-Quarter % | Month-Over-Month percentage
					"       measure '" & [Value] & " YOY %' = DIVIDE([" & [Value] & " YOY Î”], [" & [Value] & " PY])" & PercentFormat & LB &
					"       measure '" & [Value] & " QOQ %' = DIVIDE([" & [Value] & " QOQ Î”], [" & [Value] & " PQ])" & PercentFormat & LB &
					"       measure '" & [Value] & " MOM %' = DIVIDE([" & [Value] & " MOM Î”], [" & [Value] & " PM])" & PercentFormat & LB &
					
					// Previous YTD | Previous QTD | Previous MTD
					"       measure '" & [Value] & " PYTD' = CALCULATE([" & [Value] & " YTD], DATEADD(" & DateColumn & ", -1, YEAR))" & NumberFormat & LB &
					"       measure '" & [Value] & " PQTD' = CALCULATE([" & [Value] & " QTD], DATEADD(" & DateColumn & ", -1, QUARTER))" & NumberFormat & LB &
					"       measure '" & [Value] & " PMTD' = CALCULATE([" & [Value] & " MTD], DATEADD(" & DateColumn & ", -1, MONTH))" & NumberFormat & LB &
					
					// Year-Over-Year-To-Date | Quarter-Over-Quarter-To-Date | Month-Over-Month-To-Date absolute number
					"       measure '" & [Value] & " YOYTD Î”' = [" & [Value] & " YTD] - [" & [Value] & " PYTD]" & NumberFormat & LB &
					"       measure '" & [Value] & " QOQTD Î”' = [" & [Value] & " QTD] - [" & [Value] & " PQTD]" & NumberFormat & LB &
					"       measure '" & [Value] & " MOMTD Î”' = [" & [Value] & " MTD] - [" & [Value] & " PMTD]" & NumberFormat & LB &
					
					// Year-Over-Year-To-Date % | Quarter-Over-Quarter-To-Date % | Month-Over-Month-To-Date percentage
					"       measure '" & [Value] & " YOYTD %' = DIVIDE([" & [Value] & " YOYTD Î”], [" & [Value] & " PYTD])" & PercentFormat & LB &
					"       measure '" & [Value] & " QOQTD %' = DIVIDE([" & [Value] & " QOQTD Î”], [" & [Value] & " PQTD])" & PercentFormat & LB &
					"       measure '" & [Value] & " MOMTD %' = DIVIDE([" & [Value] & " MOMTD Î”], [" & [Value] & " PMTD])" & PercentFormat & LB &
					
					// YTD | QTD | MTD Over Previous Year | Quarter | Month Complete absolute number
					"       measure '" & [Value] & " YTDOPYC Î”' = [" & [Value] & " YTD] - [" & [Value] & " PYC]" & NumberFormat & LB &
					"       measure '" & [Value] & " QTDOPQC Î”' = [" & [Value] & " QTD] - [" & [Value] & " PQC]" & NumberFormat & LB &
					"       measure '" & [Value] & " MTDOPMC Î”' = [" & [Value] & " MTD] - [" & [Value] & " PMC]" & NumberFormat & LB &
					
					// YTD | QTD | MTD Over Previous Year | Quarter | Month Complete percentage
					"       measure '" & [Value] & " YTDOPYC %' = DIVIDE([" & [Value] & " YTDOPYC Î”], [" & [Value] & " PYC])" & PercentFormat & LB &
					"       measure '" & [Value] & " QTDOPQC %' = DIVIDE([" & [Value] & " QTDOPQC Î”], [" & [Value] & " PQC])" & PercentFormat & LB &
					"       measure '" & [Value] & " MTDOPMC %' = DIVIDE([" & [Value] & " MTDOPMC Î”], [" & [Value] & " PMC])" & PercentFormat & LB &
					
					// Moving Annual Total | Previous Year MAT | MAT Growth | MAT Growth %
					"       measure '" & [Value] & " MAT' = CALCULATE([" & [Value] & "], DATESINPERIOD(" & DateColumn & ", MAX(" & DateColumn & "), -12, MONTH))" & NumberFormat & LB &
					"       measure '" & [Value] & " PYMAT' = CALCULATE([" & [Value] & " MAT], DATEADD(" & DateColumn & ", -1, YEAR))" & NumberFormat & LB &
					"       measure '" & [Value] & " MATG' = [" & [Value] & " MAT] - [" & [Value] & " PYMAT]" & NumberFormat & LB &
					"       measure '" & [Value] & " MATG %' = DIVIDE([" & [Value] & " MATG], [" & [Value] & " PYMAT])" & PercentFormat,
					LB & LB
				)
			
			RETURN
				ScriptStart & ScriptMain

	annotation DAXLIB_PackageId = XU.TMDL.TimeIntelligence

	annotation DAXLIB_PackageVersion = 1.0.0